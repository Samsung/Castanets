# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

generate_jni("jni_headers") {
  sources = [
    "java/src/org/chromium/chrome/browser/safety_check/SafetyCheckBridge.java",
  ]
}

source_set("android") {
  sources = [
    "safety_check_bridge.cc",
    "safety_check_bridge.h",
  ]
  deps = [
    ":jni_headers",
    "//components/prefs",
    "//components/safety_check",
  ]
}

android_library("java") {
  sources = [
    "java/src/org/chromium/chrome/browser/safety_check/SafetyCheckBridge.java",
    "java/src/org/chromium/chrome/browser/safety_check/SafetyCheckController.java",
    "java/src/org/chromium/chrome/browser/safety_check/SafetyCheckElement.java",
    "java/src/org/chromium/chrome/browser/safety_check/SafetyCheckModel.java",
    "java/src/org/chromium/chrome/browser/safety_check/SafetyCheckSettingsFragment.java",
  ]
  deps = [
    ":java_resources",
    "//base:base_java",
    "//base:jni_java",
    "//components/browser_ui/settings/android:java",
    "//third_party/android_deps:android_support_v7_appcompat_java",
    "//third_party/android_deps:androidx_fragment_fragment_java",
    "//third_party/android_deps:androidx_preference_preference_java",
    "//ui/android:ui_full_java",
  ]
  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
  srcjar_deps = [
    ":bulk_leak_check_service_enums",
    ":safety_check_enums",
  ]
}

android_library("junit") {
  # Skip platform checks since Robolectric depends on requires_android targets.
  bypass_platform_checks = true
  testonly = true
  sources = [
    "javatests/src/org/chromium/chrome/browser/safety_check/SafetyCheckBridgeTest.java",
    "javatests/src/org/chromium/chrome/browser/safety_check/SafetyCheckModelTest.java",
  ]
  deps = [
    ":java",
    "//base:base_java",
    "//base:base_java_test_support",
    "//base:base_junit_test_support",
    "//base/test:test_support_java",
    "//chrome/browser/preferences:java",
    "//third_party/junit:junit",
    "//third_party/mockito:mockito_java",
  ]
}

android_resources("java_resources") {
  sources = [
    "java/res/layout/safety_check_button.xml",
    "java/res/values/dimens.xml",
    "java/res/xml/safety_check_preferences.xml",
  ]
  deps = [
    "//chrome/browser/ui/android/strings:ui_strings_grd",
    "//components/browser_ui/settings/android:java_resources",
    "//components/browser_ui/styles/android:java_resources",
  ]
  custom_package = "org.chromium.chrome.browser.safety_check"
}

java_cpp_enum("safety_check_enums") {
  sources = [ "//components/safety_check/safety_check.h" ]
}

java_cpp_enum("bulk_leak_check_service_enums") {
  sources = [ "//components/password_manager/core/browser/bulk_leak_check_service_interface.h" ]
}

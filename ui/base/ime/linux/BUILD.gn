# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/jumbo.gni")
import("//build/config/linux/pangocairo/pangocairo.gni")

# Allows base/ime/linux to be built if it's just for making kythe annotations.
# This is used to generate cross references in codesearch. References are
# generated by building the ozone wayland backend on ChromiumOS, which
# normally we would not want to compile base/ime/linux for. We can't build
# both ozone and x11 on Linux yet, but when we can we should remove this.
# TODO(crbug.com/1085700): Remove || enable_kythe_annotations here.
import("//build/toolchain/kythe.gni")
assert((is_linux && !is_chromeos) || enable_kythe_annotations)

jumbo_component("linux") {
  output_name = "ui_base_ime_linux"
  sources = [
    "fake_input_method_context.cc",
    "fake_input_method_context.h",
    "fake_input_method_context_factory.cc",
    "fake_input_method_context_factory.h",
    "input_method_auralinux.cc",
    "input_method_auralinux.h",
    "linux_input_method_context.h",
    "linux_input_method_context_factory.cc",
    "linux_input_method_context_factory.h",
    "text_edit_command_auralinux.cc",
    "text_edit_command_auralinux.h",
    "text_edit_key_bindings_delegate_auralinux.cc",
    "text_edit_key_bindings_delegate_auralinux.h",
  ]

  defines = [ "IS_UI_BASE_IME_LINUX_IMPL" ]

  public_deps = [ "//ui/base/ime" ]

  if (use_pangocairo) {
    configs += [ "//build/config/linux/pangocairo" ]
    sources += [
      "composition_text_util_pango.cc",
      "composition_text_util_pango.h",
    ]
  }
}
